load("@bazel_lib//lib:transitions.bzl", "platform_transition_filegroup")
load("@rules_rs//rs:rust_binary.bzl", "rust_binary")

rust_binary(
    name = "toml2json",
    srcs = ["src/main.rs"],
    crate_root = "src/main.rs",
    deps = [
        "@crates//:serde_json",
        "@crates//:serde-transcode",
        "@crates//:toml",
    ],
)

[
    platform_transition_filegroup(
        name = "for_" + triple,
        srcs = [":toml2json"],
        target_platform = "@rules_rs//rs/experimental/platforms:" + triple,
    )
    for triple in [
        "aarch64-apple-darwin",
        "x86_64-apple-darwin",
        "aarch64-unknown-linux-musl",
        "x86_64-unknown-linux-musl",
        "aarch64-pc-windows-msvc",
        "x86_64-pc-windows-msvc",
    ]
]

filegroup(
    name = "for_non_windows_platforms",
    srcs = [
        "for_aarch64-apple-darwin",
        "for_x86_64-apple-darwin",
        "for_aarch64-unknown-linux-musl",
        "for_x86_64-unknown-linux-musl",
    ],
)

platform(
    name = "local_linux_platform",
    parents = ["@platforms//host"],
    constraint_values = [
        "@toolchains_llvm_bootstrapped//constraints/libc:gnu.2.28",
    ],
)

platform(
    name = "local_windows_platform",
    parents = ["@platforms//host"],
    constraint_values = [
        "@rules_rs//rs/experimental/platforms/constraints:windows_msvc",
    ],
)
